Método multiplataforma para determinar el idioma del sistema
   Esta función suministra una cadena que representa el idioma de sistema de usuario. Funciona en Linux, Mac OS X y Windows.

 uses
 Classes, SysUtils {añade aquí las unidades adionales que necesite tu código}
 {$IFDEF win32}
  , Windows
 {$ELSE}
  , Unix
  {$IFDEF LCLCarbon}
   , MacOSAll
  {$ENDIF}
 {$ENDIF}
  ;
 function GetOSLanguage: string;
 {Método multiplataforma para determinar el idioma del sistema}
  var
   l, fbl: string;
   {$IFDEF LCLCarbon}
    theLocaleRef: CFLocaleRef;
    locale: CFStringRef;
    buffer: StringPtr;
    bufferSize: CFIndex;
    encoding: CFStringEncoding;
    success: boolean;
   {$ENDIF}
 begin
  {$IFDEF LCLCarbon}
   theLocaleRef := CFLocaleCopyCurrent;
   locale := CFLocaleGetIdentifier(theLocaleRef);
   encoding := 0;
   bufferSize := 256;
   buffer := new(StringPtr);
   success := CFStringGetPascalString(locale, buffer, bufferSize, encoding);
   if success then
    l := string(buffer^)
   else
    l := '';
   fbl := Copy(l, 1, 2);
   dispose(buffer);
  {$ELSE}
   {$IFDEF LINUX}
    fbl := Copy(GetEnvironmentVariable('LC_CTYPE'), 1, 2);
   {$ELSE}
    GetLanguageIDs(l, fbl);
   {$ENDIF}
  {$ENDIF}
  Result := fbl;
 end;